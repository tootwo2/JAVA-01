# Hello字节码分析

### 命令
1. javac -g Hello.java
2. javap -c -verbose Hello

## 分析
```
public class Hello
  minor version: 0
  major version: 52
  flags: ACC_PUBLIC, ACC_SUPER
Constant pool:
   #1 = Methodref          #13.#38        // java/lang/Object."<init>":()V
   #2 = Class              #39            // Hello
   #3 = Methodref          #2.#38         // Hello."<init>":()V
   #4 = String             #40            // aa
   #5 = Class              #41            // java/lang/StringBuilder
   #6 = Methodref          #5.#38         // java/lang/StringBuilder."<init>":()V
   #7 = Methodref          #5.#42         // java/lang/StringBuilder.append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
   #8 = String             #43            // bb
   #9 = Methodref          #5.#44         // java/lang/StringBuilder.toString:()Ljava/lang/String;
  #10 = Fieldref           #45.#46        // java/lang/System.out:Ljava/io/PrintStream;
  #11 = String             #47            // N
  #12 = Methodref          #48.#49        // java/io/PrintStream.print:(Ljava/lang/String;)V
  #13 = Class              #50            // java/lang/Object
  #14 = Utf8               <init>
  #15 = Utf8               ()V
  #16 = Utf8               Code
  #17 = Utf8               LineNumberTable
  #18 = Utf8               LocalVariableTable
  #19 = Utf8               this
  #20 = Utf8               LHello;
  #21 = Utf8               main
  #22 = Utf8               ([Ljava/lang/String;)V
  #23 = Utf8               args
  #24 = Utf8               [Ljava/lang/String;
  #25 = Utf8               hello
  #26 = Utf8               num1
  #27 = Utf8               I
  #28 = Utf8               num2
  #29 = Utf8               s1
  #30 = Utf8               Ljava/lang/String;
  #31 = Utf8               s2
  #32 = Utf8               StackMapTable
  #33 = Class              #24            // "[Ljava/lang/String;"
  #34 = Class              #39            // Hello
  #35 = Class              #51            // java/lang/String
  #36 = Utf8               SourceFile
  #37 = Utf8               Hello.java
  #38 = NameAndType        #14:#15        // "<init>":()V
  #39 = Utf8               Hello
  #40 = Utf8               aa
  #41 = Utf8               java/lang/StringBuilder
  #42 = NameAndType        #52:#53        // append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
  #43 = Utf8               bb
  #44 = NameAndType        #54:#55        // toString:()Ljava/lang/String;
  #45 = Class              #56            // java/lang/System
  #46 = NameAndType        #57:#58        // out:Ljava/io/PrintStream;
  #47 = Utf8               N
  #48 = Class              #59            // java/io/PrintStream
  #49 = NameAndType        #60:#61        // print:(Ljava/lang/String;)V
  #50 = Utf8               java/lang/Object
  #51 = Utf8               java/lang/String
  #52 = Utf8               append
  #53 = Utf8               (Ljava/lang/String;)Ljava/lang/StringBuilder;
  #54 = Utf8               toString
  #55 = Utf8               ()Ljava/lang/String;
  #56 = Utf8               java/lang/System
  #57 = Utf8               out
  #58 = Utf8               Ljava/io/PrintStream;
  #59 = Utf8               java/io/PrintStream
  #60 = Utf8               print
  #61 = Utf8               (Ljava/lang/String;)V
{
  public Hello();
    descriptor: ()V
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: return
      LineNumberTable:
        line 1: 0
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0       5     0  this   LHello;

  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=6, args_size=1
         0: new           #2                  // class Hello
         3: dup
         4: invokespecial #3                  // Method "<init>":()V
         7: astore_1
         8: bipush        10
        10: istore_2
        11: iload_2
        12: iconst_2
        13: iadd
        14: istore_3
        15: ldc           #4                  // String aa
        17: astore        4
        19: new           #5                  // class java/lang/StringBuilder
        22: dup
        23: invokespecial #6                  // Method java/lang/StringBuilder."<init>":()V
        26: aload         4
        28: invokevirtual #7                  // Method java/lang/StringBuilder.append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
        31: ldc           #8                  // String bb
        33: invokevirtual #7                  // Method java/lang/StringBuilder.append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
        36: invokevirtual #9                  // Method java/lang/StringBuilder.toString:()Ljava/lang/String;
        39: astore        5
        41: aload         5
        43: aload         4
        45: if_acmpeq     56
        48: getstatic     #10                 // Field java/lang/System.out:Ljava/io/PrintStream;
        51: ldc           #11                 // String N
        53: invokevirtual #12                 // Method java/io/PrintStream.print:(Ljava/lang/String;)V
        56: return
      LineNumberTable:
        line 5: 0
        line 8: 8
        line 9: 11
        line 12: 15
        line 13: 19
        line 15: 41
        line 16: 48
        line 19: 56
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0      57     0  args   [Ljava/lang/String;
            8      49     1 hello   LHello;
           11      46     2  num1   I
           15      42     3  num2   I
           19      38     4    s1   Ljava/lang/String;
           41      16     5    s2   Ljava/lang/String;
      StackMapTable: number_of_entries = 1
        frame_type = 255 /* full_frame */
          offset_delta = 56
          locals = [ class "[Ljava/lang/String;", class Hello, int, int, class java/lang/String, class java/lang/String ]
          stack = []
}
```

### 概要
1. `major version: 52`表示使用的java版本是jdk8
2. `Constant pool`表示常量池
3. `public Hello()`之后的表示类默认的构造函数
4. `public static void main(java.lang.String[]);`开始表示main函数
5. `descriptor: ([Ljava/lang/String;)V`表示参数,'['表示数组,'L'表示对象
6. `stack=2, locals=6, args_size=1`栈大小为2,局部变量数为6,参数个数为1

## 分析main函数内部
java代码:
```
  Hello hello = new Hello();
```
对应的字节码
```
  0: new           #2                  // class Hello
  3: dup
  4: invokespecial #3                  // Method "<init>":()V
  7: astore_1
```
*解释*
- 指令new,创建对象,但没有调用构造函数,'#2'表示常量池中找到Class
- 指令dup,复制栈顶的值(构造函数无返回值)
- 指令invokespecial,调用构造函数

java代码:
```
  int num1 = 10;
  int num2 = num1 + 2;
```
对应的字节码
```
  8: bipush        10
  10: istore_2
  11: iload_2
  12: iconst_2
  13: iadd
  14: istore_3
```
*解释*
- bipush: byte型常量进栈
- istore_2: 栈顶int数值存入第3个局部变量
- iload_2: 读取第3个局部变量
- iconst_2: int型常量2进栈
- iadd: 操作数栈上的两个数值相加
- istore_3: 栈顶int数值存入第4个局部变量

java代码:
```
  String s1 = "aa";
  String s2 = s1 + "bb";
```
对应字节码
```
  15: ldc           #4                  // String aa
  17: astore        4
  19: new           #5                  // class java/lang/StringBuilder
  22: dup
  23: invokespecial #6                  // Method java/lang/StringBuilder."<init>":()V
  26: aload         4
  28: invokevirtual #7                  // Method java/lang/StringBuilder.append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
  31: ldc           #8                  // String bb
  33: invokevirtual #7                  // Method java/lang/StringBuilder.append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
  36: invokevirtual #9                  // Method java/lang/StringBuilder.toString:()Ljava/lang/String;
  39: astore        5
```
*解释*
- ldc: 从常量池中将位置为#4的常量推送至栈顶
- astore  4: 栈顶对象ref存入第5个局部变量表
- 19 - 39: 字符串拼接,实例化StringBuilder对象,加载局部变量池的第5个变量放入栈顶,调用append方法,加在常量池#8的常量放入栈顶,调用append方法,最后调用toString方法,astore将对象ref放入局部变量表的第6个位置

java代码:
```
  if(s2 != s1){
    System.out.print("N");
  }
```
对应字节码
```
  41: aload         5
  43: aload         4
  45: if_acmpeq     56
  48: getstatic     #10                 // Field java/lang/System.out:Ljava/io/PrintStream;
  51: ldc           #11                 // String N
  53: invokevirtual #12                 // Method java/io/PrintStream.print:(Ljava/lang/String;)V
```
*解释*
- aload 5: 讲局部变量表第6个对象ref压入栈顶
- aload 4: 讲局部变量表第5个对象ref压入栈顶
- if_acmpeq: 比较栈顶两个元素,当结果相等时跳转至56
- getstatic: 获取System类的静态域, 并将其压入栈顶
- ldc: 从常量池中将位置为#11的常量推送至栈顶
- invokevirtual #12: 调用PrintStream.print